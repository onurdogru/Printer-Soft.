@ECHO OFF

@SET EXIT_CODE=0
REM LOW_HIGH_EXT
SET FUSE=FFD1FF
SET SW_FILE=68EI.hex
SET SW_TOOL="C:\Program Files (x86)\Atmel\Studio\7.0\atbackend\atprogram"
SET freq[0]=2mhz
SET freq[1]=1mhz
SET freq[2]=500khz
SET freq[3]=250khz
SET freq[4]=125khz
SET i=0
SET len=5

@ECHO.
@ECHO PROCESS NAME: SEGER 68E ATMEL FCT PROGRAMMING
@ECHO.

@ECHO PROCESS 1: CHIPERASE, SET FUSE SETTINGS, VERIFY
@ECHO.
%SW_TOOL% -t atmelice -i updi -cl 8khz -d attiny202 chiperase write --verify -fs --values %FUSE%
@IF ERRORLEVEL ==1 GOTO ERROR1

:PROCESS2
@IF %i% EQU %len% GOTO ERROR2
@ECHO.
SETLOCAL enableDelayedExpansion
@ECHO PROCESS 2: PROGRAM MCU, VERIFY, !freq[%i%]!
%SW_TOOL%  -t atmelice -i updi -cl !freq[%i%]! -d attiny202 program --verify -f %SW_FILE%
ENDLOCAL

@IF '%ERRORLEVEL%'=='0' (
	GOTO OK
) ELSE (
	SET /A i=%i%+1 
	GOTO PROCESS2
)

:OK
@ECHO.
@ECHO RESULT: COMPLETED SUCCESSFULLY
@SET EXIT_CODE=0
@GOTO END

:ERROR1
@ECHO. 
@ECHO RESULT: FAILED
@ECHO ERROR: PROCESS 1, ERRORLEVEL: %ERRORLEVEL%
@SET EXIT_CODE=1
@GOTO END

:ERROR2
@ECHO. 
@ECHO RESULT: FAILED
@ECHO ERROR: PROCESS 2, ERRORLEVEL: %ERRORLEVEL%
@SET EXIT_CODE=2
@GOTO END

:END
@ECHO. 
@ECHO EXIT_CODE: %EXIT_CODE%
@PAUSE
EXIT %EXIT_CODE%